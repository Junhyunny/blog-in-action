plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'action.in.blog'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    jaxb
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/main/jaxb' // 소스 코드 경로로 src/main/jaxb 추가
        }
    }
}

extensions.findByName("buildScan")?.with {
    setProperty("termsOfServiceUrl", "https://gradle.com/terms-of-service")
    setProperty("termsOfServiceAgree", "yes")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web-services' // Spring Web Service
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation 'wsdl4j:wsdl4j' // WSDL 처리 모듈
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0' // JDK17 관련 에러 보완
    implementation 'org.apache.ws.xmlschema:xmlschema-core:2.2.1' // Apache Web Service URIResolver 관련 에러 보완

    jaxb "org.glassfish.jaxb:jaxb-xjc" // OXM
}

tasks.named('test') {
    useJUnitPlatform()
}

// XSD 파일을 기준으로 Java 클래스를 생성하는 태스크(task)
tasks.register('genJaxb') {
    // XSD 파일을 Java 클래스로 변환하여 jaxb 폴더에 위치시킵니다.
    ext.sourcesDir = "src/main/jaxb"
    ext.schema = "src/main/resources/xsd"

    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            xjc(destdir: sourcesDir) {
                arg(value: "-wsdl")
                schema(dir: schema, includes: "**/*.xsd")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}